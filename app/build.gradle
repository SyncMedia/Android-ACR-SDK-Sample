apply plugin: 'com.android.application'

static def getGitVersion() {
    def cmd = "git describe --tag --abbrev=0"
    def proc = cmd.execute()
    return proc.text.trim()
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.2"

    defaultConfig {
        applicationId "com.syncmedia.audiodemo"
        minSdkVersion 16
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        ndk {
            // Tells Gradle to build outputs for the following ABIs and package
            // them into your APK.
            abiFilters 'x86', 'x86_64', 'armeabi', 'arm64-v8a', 'armeabi-v7a'
        }
    }

    lintOptions {
        checkReleaseBuilds true
        abortOnError false
    }

    buildTypes {
        release {
            minifyEnabled false
            debuggable false
            jniDebuggable false
            zipAlignEnabled true
        }

        debug {
            ext.enableCrashlytics = false
            minifyEnabled false
            debuggable true
        }
    }

    flavorDimensions "default"

    productFlavors {
        development {
            dimension "default"
            versionName getGitVersion() + "-SNAPSHOT"
        }

        production {
            dimension "default"
            versionName getGitVersion()
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    jcenter()
    mavenCentral()
    maven { url "https://s3.amazonaws.com/repo.commonsware.com" }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':android_sync_sdk')

    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'testfairy:testfairy-android-sdk:1.9.21@aar'

    //sdk dependencies
    implementation 'com.getkeepsafe.relinker:relinker:1.3.1'
    implementation "androidx.room:room-runtime:2.2.2"
    annotationProcessor "androidx.room:room-compiler:2.2.2"
}